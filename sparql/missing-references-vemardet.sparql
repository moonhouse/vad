# Persons described by source "Vem Ã¤r det" but missing reference to source
SELECT DISTINCT ?item ?itemLabel ?edition ?editionLabel ?fodd ?dod WHERE {
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE]". }
  {
    SELECT DISTINCT ?item ?edition ?fodd ?dod WHERE {
      ?item wdt:P31 wd:Q5;
        wdt:P27 wd:Q34;
        wdt:P569 ?fodd.
      OPTIONAL { ?item wdt:P570 ?dod. }
      {
        ?item p:P1343 ?describedBySource.
        ?describedBySource ps:P1343 ?edition.
        ?edition wdt:P629 wd:Q5637701.
        FILTER(NOT EXISTS { ?describedBySource prov:wasDerivedFrom ?reference. })
      }
    }
    LIMIT 100
  }
}
