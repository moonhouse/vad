# Persons described by source "Vem Ã¤r vem" but missing reference to source
SELECT DISTINCT ?item ?itemLabel ?part ?partLabel ?fodd ?dod WHERE {
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE]". }
  {
    SELECT DISTINCT ?item ?part ?fodd ?dod WHERE {
      ?item wdt:P31 wd:Q5;
        wdt:P27 wd:Q34;
        wdt:P569 ?fodd.
      OPTIONAL { ?item wdt:P570 ?dod. }
      {
        ?item p:P1343 ?describedSource.
        ?describedSource ps:P1343 ?part.
        ?part wdt:P361 ?edition.
        ?edition wdt:P629 wd:Q19752305.
        FILTER(NOT EXISTS { ?describedSource prov:wasDerivedFrom ?reference. })
      }
    }
    LIMIT 100
  }
}
